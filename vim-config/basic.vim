" ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ãªã©ã«ä¾å­˜ã—ãªã„è¨­å®š

if &compatible
  set nocompatible
endif

function! s:auto_mkdir(dir, force)
  if !isdirectory(a:dir) && (a:force ||
  \    input(printf('"%s" does not exist. Create? [y/N] ', a:dir)) =~? '^y\%[es]$')
    call mkdir(iconv(a:dir, &encoding, &termencoding), 'p')
  endif
endfunction

" Shell
" ----------------------------

" Windowsã§ã¯Powershell Coreã‚’ä½¿ã†
if has('win32')
  set shell=pwsh
endif

" Encoding
" -----------------------------

set encoding=utf-8
set fileencoding=utf-8
set termencoding=utf-8
set fileencodings=utf-8,cp932,euc-jp
set ambiwidth=single

" File
" -----------------------------

set autoread
set fileformats=unix,dos

" Backup
" -----------------------------

call s:auto_mkdir(expand('$HOME/.vim/backup'), 0)
call s:auto_mkdir(expand('$HOME/.vim/undofile'), 0)
set browsedir=buffer
set directory=$HOME/.vim/backup
set history=1000
set undodir=$HOME/.vim/undofile
set backupdir=$HOME/.vim/backup
set backup

" Search
" ------------------------------

set incsearch
set hlsearch
set ignorecase
set smartcase
set wrapscan

" Input
" -------------------------------

set expandtab
set textwidth=0
set belloff=all
set autoindent
set backspace=indent,eol,start
set updatetime=300
set showmatch
set matchtime=1
set matchpairs+=<:>
set ttimeoutlen=10 " ESCã‚­ãƒ¼ã®é…å»¶ã‚’è§£æ¶ˆ

" View
" -------------------------------

set hidden
set pumheight=10
set noshowmode
set relativenumber
set laststatus=2
set splitbelow
set shortmess+=c
set cmdheight=1
set completeopt=menu
if !has('nvim')
set ballooneval
set showcmd
set foldmethod=marker
else
set inccommand=split
endif

" ãƒ•ã‚¡ã‚¤ãƒ«ã‚¿ã‚¤ãƒ—ã”ã¨ã®è¨­å®š
" -------------------------------

set breakindent

" ã‚¿ãƒ–å¹…ã‚’è¨­å®šã™ã‚‹
function! s:set_tabwidth(width) abort
  let &l:tabstop = a:width
  let &l:shiftwidth = a:width
  let &l:softtabstop = a:width
endfunction

function s:set_rust_options()
  setlocal textwidth=100
  setlocal colorcolumn=+1
endfunction

function s:set_ocaml_options()
  setlocal textwidth=100
  setlocal colorcolumn=+1
  call s:set_tabwidth(2)
endfunction

function s:set_python_options()
  setlocal textwidth=100
  setlocal colorcolumn=+1
endfunction

" ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã®ã‚¿ãƒ–å¹…
let s:def_tabwidth = 4
let &tabstop = s:def_tabwidth
let &shiftwidth = s:def_tabwidth
let &softtabstop = s:def_tabwidth

" ãƒ•ã‚¡ã‚¤ãƒ«ã‚¿ã‚¤ãƒ—ã”ã¨ã®è¨­å®š
augroup SettingsPerFileType
  autocmd!
  " ã‚¿ãƒ–å¹…ã‚’2ã«ã™ã‚‹
  autocmd FileType vim,ruby,nim,toml,json,yaml,vue,javascript,typescript,html,pug,jinja,sml,css call s:set_tabwidth(2)

  autocmd FileType rust call s:set_rust_options()
  autocmd FileType python call s:set_python_options()
  autocmd FileType ocaml call s:set_ocaml_options()
augroup END

" æ‹¡å¼µå­ã‚’å…ƒã«filetypeã‚’è¨­å®šã™ã‚‹
augroup FileTypeFromExtension
  autocmd!
  autocmd BufNewFile,BufRead *.ejs set ft=html
  autocmd BufNewFile,BufRead *.html.tera set ft=jinja.html
augroup END

" ç¾åœ¨ã®ãƒãƒƒãƒ•ã‚¡ãŒã‚¿ãƒ¼ãƒŸãƒŠãƒ«ã ã£ãŸã‚‰è¡Œç•ªå·ã‚’éš ã™
augroup TerminalNumber
  autocmd!

  function! s:hide_linenumber_if_terminal()
    if &buftype == 'terminal'
      set nonumber
    else
      set nonumber
    endif
  endfunction

  " FIXME: ã“ã‚Œã‚’è¿½åŠ ã™ã‚‹ã¨èµ·å‹•æ™‚ã«Vimã®ã‚¿ã‚¤ãƒˆãƒ«ãŒè¡¨ç¤ºã•ã‚Œãªã„
  " autocmd BufEnter * call timer_start(0, { -> s:hide_linenumber_if_terminal() })
augroup END

augroup MkdirWhenWrite
  " ãƒ•ã‚¡ã‚¤ãƒ«æ›¸ãè¾¼ã¿æ™‚ã«ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªãŒå­˜åœ¨ã—ãªã‹ã£ãŸã‚‰ä½œæˆã™ã‚‹ã‹ã©ã†ã‹èã
  autocmd BufWritePre * call s:auto_mkdir(expand('<afile>:p:h'), v:cmdbang)
augroup END

" Leader
" -------------------------------------

" leaderã‚’ã‚¹ãƒšãƒ¼ã‚¹ã‚­ãƒ¼ã«è¨­å®š
noremap <leader> <nop>
noremap <LocalLeader> <nop>
let g:mapleader = "\<Space>"
let g:mamplocalleader = '\'

" Shortcuts
" --------------------------------------

" C/C++ã®ã‚½ãƒ¼ã‚¹ãƒ•ã‚¡ã‚¤ãƒ«ã¨ãƒ˜ãƒƒãƒ€ãƒ•ã‚¡ã‚¤ãƒ«ã‚’åˆ‡ã‚Šæ›¿ãˆã‚‹
function! ToggleFile()
  let l:source_exts = ['cpp', 'c', 'cxx']
  let l:header_exts = ['h', 'hpp', 'hxx']

  let l:ext = expand('%:e')
  let l:curfile = expand('%:p:r')

  " Header
  let l:index = index(l:header_exts, l:ext)
  if l:index >= 0
    for ext in l:source_exts
      let l:filename = l:curfile . '.' . ext
      if !empty(glob(l:filename))
        execute 'e ' . l:filename
        return
      endif
    endfor

    execute 'e ' . l:curfile . '.' . l:source_exts[0]
    return
  endif

  " Source
  let l:index = index(l:source_exts, l:ext)
  if l:index >= 0
    for ext in l:header_exts
      let l:filename = l:curfile . '.' . ext
      if !empty(glob(l:filename))
        execute 'e ' . l:filename
        return
      endif
    endfor

    execute 'e ' . l:curfile . '.' . l:header_exts[0]
    return
  endif
endfunction

" TODO: 
" (a|, b, c)
" (a|), (b, c), d)
" (a, '(', c) ã‚¢ãƒã‚¹ãƒˆãƒ­ãƒ•ã‚£ãŒè­˜åˆ¥å­ã«ä½¿ãˆã‚‹è¨€èªã‚‚ã‚ã‚‹
function! BackwardArg() abort
endfunction

function! ForwardArg() abort
endfunction

" ç¾åœ¨è¡Œã® Vim script ã‚’å®Ÿè¡Œã™ã‚‹
nnoremap <leader>ve :exec getline('.')<CR>
" ã™ã¹ã¦ã®ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ã‚’æ¶ˆã™
nnoremap <silent> <leader>q :call popup_clear()<CR>
" JISé…åˆ—ã®ã¨ãã‚‚USé…åˆ—ã¨åŒã˜ä½ç½®ã§ESCã§ãã‚‹ã‚ˆã†ã«ã™ã‚‹
inoremap <C-@> <ESC>
" USé…åˆ—ã§ã¯@ã‚­ãƒ¼ãŒæŠ¼ã—ã¥ã‚‰ã„
nnoremap U @
" C/C++ã®ã‚½ãƒ¼ã‚¹ãƒ•ã‚¡ã‚¤ãƒ«ã¨ãƒ˜ãƒƒãƒ€ãƒ•ã‚¡ã‚¤ãƒ«ã‚’åˆ‡ã‚Šæ›¿ãˆã‚‹
nnoremap <silent> <leader>f :call ToggleFile()<CR>
" ã‚¿ãƒ–é–¢é€£
nnoremap <leader>t :tabnew<CR>
nnoremap <C-h> gT
nnoremap <C-l> gt
" æ¤œç´¢ã‚­ãƒ¼ãƒ¯ãƒ¼ãƒ‰ã‚’å‰Šé™¤ï¼ˆãƒã‚¤ãƒ©ã‚¤ãƒˆã‚‚æ¶ˆãˆã‚‹ï¼‰
nnoremap <silent> <leader>n :let @/ = ''<CR>
" å‰å¾Œã®å¼•æ•°ã«ç§»å‹•ã™ã‚‹
nnoremap <silent> [ BackwardArg()
nnoremap <silent> ] ForwardArg()

" Aliases
" --------------------------------

" ã‚«ãƒ¼ã‚½ãƒ«ä¸Šã®ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚°ãƒ«ãƒ¼ãƒ—ã‚’è¡¨ç¤ºã™ã‚‹
command! Hg echo synIDattr(synIDtrans(synID(line('.'), col('.'), 1)), 'name')
" ãƒãƒƒãƒ•ã‚¡å†…ã®ANSIåˆ¶å¾¡æ–‡å­—ã‚’é™¤å»
command! DeleteAnsi %s/\[[0-9;]*m//g
" æ‰“ã¡é–“é•ã„
command! W w

" Syntax
" ---------------------------------

" Doxygenã®ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’æœ‰åŠ¹ã«ã™ã‚‹
let g:load_doxygen_syntax = 1

" jqã‚’å®Ÿè¡Œã—ã¦JSONã‚’ãƒ•ã‚©ãƒ¼ãƒãƒƒãƒˆ
command! -nargs=? Jq call s:Jq(<f-args>)
function! s:Jq(...)
  if 0 == a:0
    let l:arg = "."
  else
    let l:arg = a:1
  endif
  execute "%! jq \"" . l:arg . "\""
endfunction

" tabline
" --------------------------------

function! s:tabpage_label(n)
  let hi = a:n is tabpagenr() ? '%#TabLineSel#' : '%#TabLine#'

  " ã‚¿ãƒ–ãƒšãƒ¼ã‚¸å†…ã®ãƒãƒƒãƒ•ã‚¡ã®ãƒªã‚¹ãƒˆ
  let bufnrs = tabpagebuflist(a:n)

  let mod = len(filter(copy(bufnrs), 'getbufvar(v:val, "&modified")')) ? ' +' : ''

  " ã‚«ãƒ¬ãƒ³ãƒˆãƒãƒƒãƒ•ã‚¡ã®ãƒ•ã‚¡ã‚¤ãƒ«å
  let curbufnr = bufnrs[tabpagewinnr(a:n) - 1]
  let fname = fnamemodify(bufname(curbufnr), ':t')
  if fname == ''
    let fname = '[ç„¡å]'
  endif

  let label = a:n . ' ' . fname . mod

  return '%' . a:n . 'T' . hi . ' ' . label . ' %T%#TabLineFill#'
endfunction

function! MakeTabLine()
  let titles = map(range(1, tabpagenr('$')), 's:tabpage_label(v:val)')
  let tabpages = join(titles, '') . '%#TabLineFill#%T'

  " é¸æŠã—ã¦ã„ã‚‹ã‚¿ãƒ–ãƒšãƒ¼ã‚¸ã®ã‚«ãƒ¬ãƒ³ãƒˆãƒãƒƒãƒ•ã‚¡ã®ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒª
  let bufnrs = tabpagebuflist(tabpagenr())
  let curbufnr = bufnrs[tabpagewinnr(tabpagenr()) - 1]
  let dname = fnamemodify(bufname(curbufnr), ':p:h')
  let cwd = fnamemodify(dname, ":~")
  if has('win32')
    let cwd = substitute(cwd, escape($USERPROFILE, '\'), 'W~', '')
    let cwd = substitute(cwd, '\\', '/', 'g')
  endif

  let info = cwd
  return tabpages . '%=' . info
endfunction

set tabline=%!MakeTabLine()

function! s:highlight()
  " tablineã®ä¸‹ç·šã‚’ç„¡åŠ¹ã«ã™ã‚‹
  highlight TabLine term=NONE gui=NONE
  " tablineã®å¤ªå­—ã‚’ç„¡åŠ¹ã«ã™ã‚‹
  highlight TabLineSel term=NONE gui=NONE
endfunction

autocmd! ColorScheme * call s:highlight()
